plugins {
	id 'fabric-loom' version '1.0-SNAPSHOT'
	id 'maven-publish'
	id "com.modrinth.minotaur" version "2.+"
	id "com.matthewprenger.cursegradle" version "1.4.0"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name + "-${minecraft_version}"
version = project.mod_version
group = project.maven_group

repositories {
	maven { url "https://maven.shedaniel.me/" }
	maven { url "https://maven.terraformersmc.com/releases/" }
}


dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}

	modApi "com.terraformersmc:modmenu:${project.mod_menu_version}"

}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}


project.ext.changelog = project.changelog
project.ext.versionName = "[1.19.3] HitBox+ ${project.version}"
project.ext.debug = true

modrinth {
	token = System.getenv("MODRINTH_TOKEN") // This is the default. Remember to have the MODRINTH_TOKEN environment variable set or else this will fail, or set it to whatever you want - just make sure it stays private!
	projectId = project.archives_base_name // This can be the project ID or the slug. Either will work!
	versionNumber = "${project.minecraft_version}-${project.version}" // You don't need to set this manually. Will fail if Modrinth has this version already
	versionName = project.ext.versionName
	versionType = project.release_type // This is the default -- can also be `beta` or `alpha`
	uploadFile = remapJar // With Loom, this MUST be set to `remapJar` instead of `jar`!
	gameVersions = ["1.19.3"] // Must be an array, even with only one version
	loaders = ["fabric"] // Must also be an array - no need to specify this if you're using Loom or ForgeGradle
	dependencies { // A special DSL for creating dependencies
		// scope.type
		// The scope can be `required`, `optional`, `incompatible`, or `embedded`
		// The type can either be `project` or `version`
		optional.project "modmenu"
		required.project "cloth-config"
	}
	syncBodyFrom = rootProject.file("README.md").text
	changelog = project.ext.changelog
	debugMode = project.ext.debug
}

tasks.modrinth.dependsOn(tasks.modrinthSyncBody)



//curseforge {
//	apiKey = System.getenv("CURSEFORGE_TOKEN")
//	project {
//		id = '690221'
//		changelog = project.ext.changelog
//		releaseType = project.release_type
//		addGameVersion "1.19.3"
//		addGameVersion "Fabric" //Set Modloader to Fabric
//		afterEvaluate {
//			mainArtifact(remapJar)
//			uploadTask.dependsOn(remapJar)
//
//		}
//		mainArtifact(remapJar) {
//			displayName = project.ext.versionName
//			relations {
//				optionalDependency "modmenu"
//				requiredDependency "cloth-config"
//			}
//		}
//	}
//	options {
//		debug = project.ext.debug
//		forgeGradleIntegration = false
//	}
//}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
